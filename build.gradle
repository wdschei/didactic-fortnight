group = 'com.thescheideggers'
def baseVersion = '0.0.1'
version = project.hasProperty('release') ? baseVersion : "$baseVersion-SNAPSHOT"
description = "A Gradle plugin to teach others how to make a Gradle plugin"

buildscript {
    repositories {
        gradlePluginPortal()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'gradle.plugin.org.ysb33r.gradle:gradletest:1.1'
        classpath 'org.owasp:dependency-check-gradle:3.3.2'
    }
}

apply plugin: 'groovy'
apply plugin: 'org.ysb33r.gradletest'
apply plugin: 'org.owasp.dependencycheck'

repositories {
    jcenter()
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile 'org.codehaus.groovy:groovy-all:2.4.15'
    testCompile 'junit:junit:4.12'
    gradleTest 'com.google.guava:guava:18.0'
    gradleTest 'commons-validator:commons-validator:1.4.0'
    gradleTest 'org.jsoup:jsoup:1.8.2'
}

task unversionedJar(type: Jar, dependsOn: 'jar') {
    version = null
    from sourceSets.main.output
}

task installPlugin(type: Copy, dependsOn: 'unversionedJar') {
    def gradleInstallDir = gradle.gradleHomeDir
    group = 'installation'
    description = 'Installs the plugin jar in your gradle distribution.'
    from "${buildDir}/libs/${unversionedJar.archiveName}"
    into "/${gradleInstallDir}/lib/plugins"
    doLast {
        println "Installed in: ${gradleInstallDir}/lib/plugins as: ${unversionedJar.archiveName}"
    }
}

task uninstallPlugin(type: Delete) {
    def gradleInstallDir = gradle.gradleHomeDir
    group = 'installation'
    description = 'Removes the plugin from your gradle distribution.'
    delete("/${gradleInstallDir}/lib/plugins/${unversionedJar.archiveName}")
}

tasks.withType(Groovydoc).all { enabled = false }

gradleTest {
    dependsOn jar

    copy {
        from(jar)
        into("${project.buildDir}/gradleTest/repo/")
    }

    // NOTE: Support for testing Gradle < v3.0 will be removed from GradleTest v2.x.
    versions '2.0', '2.1', '2.2', '2.3', '2.4', '2.5', '2.6', '2.7', '2.8', '2.9', '2.10', '2.11', '2.12', '2.13', '2.14.1'
    versions '3.0', '3.1', '3.2.1', '3.3', '3.4.1', '3.5.1'
    versions '4.0', '4.1', '4.2', '4.3.1', '4.4', '4.5', '4.5.1', '4.6', '4.7', '4.8', '4.8.1', '4.9', '4.10', '4.10.1', '4.10.2'

    beforeTest {
        println "  ${it.name}"
    }
}

// NOTE: See the link below for a better understanding on the relationship of the tasks provided by the Java plugin.
//       > https://docs.gradle.org/current/userguide/java_plugin.html#sec:java_tasks
check.dependsOn tasks.gradleTest
